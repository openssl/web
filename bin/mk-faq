#! /usr/bin/perl
use strict;
use warnings;

# Filename->anchor name
my %anchors;
foreach my $f ( @ARGV ) {
    next unless $f =~ /faq-[0-9]-(.*).txt/;
    $anchors{$f} = uc($1);
}

my $top = '  <a href="#toc"><img src="/img/up.gif"/></a>';
print "<h3><a name='toc'>Table of Contents</a></h3>\n";

# Print the TOC
foreach my $f ( @ARGV ) {
    my $a = $anchors{$f};
    open(IN, "<$f") || die "Can't open $f, $!";
    my $title = <IN>;
    $title =~ s|\R$||;
    print "\n  <h4><a href='#$a'>$title</a></h4>\n";
    my $i = 1;
    print "  <ol>\n";
    while ( <IN> ) {
        s|\R$||;
        next unless /^\* /;
        s/^\* //;
        print "    <li><a href='#$a$i'>$_</a></li>\n";
        $i++;
    }
    print "  </ol>\n";
    close(IN);
}

# Print the contents.
foreach my $f ( @ARGV ) {
    my $a = $anchors{$f};
    open(IN, "<$f") || die "Can't open $f, $!";
    my $title = <IN>;
    $title =~ s|\R$||;
    print "\n\n<hr>\n";
    print "<h3><a name='$a'>$title</a>$top</h3>\n";
    my $i = 1;
    my $pre = 0;
    while ( <IN> ) {
        s|\R$||;
        # Comments lines start with #
        next if /^#/;
        if ( /^\* / ) {
            # New item: "* question text..."
            s/\* //;
            print "\n<h4><a name='$a$i'>$i. $_</a>$top</h4>\n";
            $i++;
        } elsif ( m@<PRE>@i ) {
            # Start of a <pre> section
            die "Nested <PRE> in $f" if $pre != 0;
            print "<pre>\n";
            $pre = 1;
        } elsif ( m@</PRE>@i ) {
            # End of a <pre> section
            die "Unbalanced </PRE> in $f" if $pre != 1;
            print "</pre>\n";
            $pre = 0;
        } elsif ( $pre ) {
            # If in <pre> state, just print
            print "$_\n";
        } elsif ( /^$/ ) {
            # Blank lines separate paragraphs
            print "<p>\n";
        } elsif ( /(.*)@@@(.*)@@@(.*)/ ) {
            # URL: @@@http:....@@@
            print "$1<a href='$2'>$2</a>$3\n";
        } elsif ( /([-\.\w]+)\((\d)\)/ ) {
            # Manpage "foo(1)"
            my ($page, $sec) = ($1, $2);
            my $link = "/docs/manmaster/man$sec/$page.html";
            s|$page\($sec\)|<a href="$link">$page($sec)</a>|;
            print "$_\n";
        } else {
            # Ordinary line.
            print "$_\n";
        }
    }
    close(IN);
    die "Unclosed <PRE> in $f" if $pre != 0;
}

exit(0);
